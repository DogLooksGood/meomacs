#+title: Editor Basics

Configuration for editor basics.

#+begin_src emacs-lisp :exports none
  ;;; -*- lexical-binding: t -*-
#+end_src

* Package management                                               :required:

** Straight

Straight.el is used for package management.

*** Disable native compilation

#+begin_src emacs-lisp
  (setq straight-disable-native-compile t)
#+end_src

*** Key bindings

#+tblname: straight-keymap-table
| Key | Command                  |
|-----+--------------------------|
| c   | straight-check-package   |
| C   | straight-check-all       |
| p   | straight-pull-package    |
| P   | straight-pull-all        |
| b   | straight-rebuild-package |
| B   | straight-rebuild-all     |
| v   | straight-freeze-versions |

#+header: :var straight-keymap-table=straight-keymap-table
#+begin_src emacs-lisp
  (meomacs-keymap-table straight-keymap straight-keymap-table)
  (global-set-key (kbd "C-c P") 'straight-keymap)
#+end_src

* Better defaults

#+begin_src emacs-lisp
  (setq-default

   inhibit-startup-screen t
   ;; Don't display comp warnings
   warning-suppress-log-types '((comp))
   ;; Don't create lockfiles
   create-lockfiles nil

   ;; prefer UTF8
   buffer-file-coding-system 'utf-8-unix
   default-file-name-coding-system 'utf-8-unix
   default-keyboard-coding-system 'utf-8-unix
   default-process-coding-system '(utf-8-unix . utf-8-unix)
   default-sendmail-coding-system 'utf-8-unix
   default-terminal-coding-system 'utf-8-unix

   ;; Add newline at bottom of file
   require-final-newline t

   ;; Backup setups
   ;; We use temporary directory /tmp for backup files
   ;; More versions should be saved
   backup-directory-alist `((".*" . ,temporary-file-directory))
   auto-save-file-name-transforms `((".*" ,temporary-file-directory t))
   backup-by-copying t
   delete-old-versions t
   kept-new-versions 6
   kept-old-versions 2
   version-control t

   ;; Skip prompt for xref find definition
   xref-prompt-for-identifier nil

   ;; Don't wait for keystrokes display
   echo-keystrokes 0.01

   ;; Disable margin for overline and underline
   overline-margin 0
   underline-minimum-offset 0

   ;; Allow resizing frame by pixels
   frame-resize-pixelwise t

   ;; Better scroll behavior
   mouse-wheel-scroll-amount '(1 ((shift) . 1) ((control) . nil))
   mouse-wheel-progressive-speed nil

   ;; Disable copy region blink
   copy-region-blink-delay 0

   ;; Use short answer when asking yes or no
   read-answer-short t

   ;; Always follow link
   vc-follow-symlinks t

   ;; Use custom.el as custom file
   custom-file (expand-file-name "custom.el" user-emacs-directory)

   ;; Disable ring bell
   ring-bell-function 'ignore

   ;; Mouse yank at current point
   mouse-yank-at-point t

   ;; DWIM target for dired
   ;; Automatically use another dired buffer as target for copy/rename
   dired-dwim-target t

   ;; Use spaces instead of tab
   indent-tabs-mode nil

   ;; Don't echo multiline eldoc
   eldoc-echo-area-use-multiline-p nil

   ;; No cursor blink in TUI
   visible-cursor nil

   ;; Counter for isearch
   isearch-lazy-count t
   isearch-lazy-highlight t

   compilation-scroll-output 'first-error

   ;; Flatten imenu for easy searching
   imenu-flatten t

   ;; For scrolling performance
   fast-but-imprecise-scrolling t
   inhibit-compacting-font-caches t
   auto-window-vscroll nil)

  (scroll-bar-mode -1)

  (global-subword-mode 1)
  (diminish 'subword-mode)

  (blink-cursor-mode -1)

  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

* Start a emacs server

#+begin_src emacs-lisp
  (require 'server)
  (unless (server-running-p)
    (server-start))
#+end_src

* Meow

#+begin_src emacs-lisp
  (straight-use-package 'meow)

  (require 'meow)

  ;; hide lighters
  (diminish 'meow-normal-mode)
  (diminish 'meow-motion-mode)
  (diminish 'meow-insert-mode)
  (diminish 'meow-keypad-mode)
  (diminish 'meow-beacon-mode)
#+end_src

** Load key bindings

Key bindings are defined in [[file:private.org::#Modal Editing Key Binding][private.org]].

#+header: :var normal-keybindings=private.org:normal-keybindings
#+header: :var motion-keybindings=private.org:motion-keybindings
#+header: :var leader-keybindings=private.org:leader-keybindings
#+begin_src emacs-lisp
  (defun meow-setup ()
    (let ((parse-def (lambda (x)
                       (cons (string-trim (format "%s" (car x)) "[[:space:]]" "[[:space:]]")
                             (if (string-prefix-p "dispatch:" (cadr x))
                                 (string-trim (substring (cadr x) 9))
                               (intern (cadr x)))))))
      (apply #'meow-normal-define-key (mapcar parse-def normal-keybindings))
      (apply #'meow-motion-overwrite-define-key (mapcar parse-def motion-keybindings))
      (apply #'meow-leader-define-key (mapcar parse-def leader-keybindings))))
#+end_src

** Basic configurations
#+begin_src emacs-lisp
  ;; custom variables
  (setq meow-esc-delay 0.001)
  (meow-thing-register 'angle
                     '(pair ("<") (">"))
                     '(pair ("<") (">")))
  (add-to-list 'meow-char-thing-table
               '(?h . angle))
  (meow-setup)
  (meow-setup-indicator)
  (unless (bound-and-true-p meow-global-mode)
    (meow-global-mode 1))
#+end_src

** Use shorten indicators

#+begin_src emacs-lisp
  (setq meow-replace-state-name-list
        '((normal . "N")
          (beacon . "B")
          (insert . "I")
          (motion . "M")
          (keypad . "K")))
#+end_src

** Allow all C-c <letter> for leader

#+begin_src emacs-lisp
  (setq meow-keypad-leader-dispatch "C-c")
#+end_src

** Tree-sitter integration

#+begin_src emacs-lisp
  (straight-use-package 'meow-tree-sitter)

  (require 'meow-tree-sitter)

  (defvar meomacs--register-tree-sitter-run-once nil)
  (unless meomacs--register-tree-sitter-run-once
    (meow-tree-sitter-register-defaults)
    (setq meomacs--register-tree-sitter-run-once t))
#+end_src

* Window management

Switch window by pressing the number showned in the mode line.

#+begin_src emacs-lisp
  (straight-use-package '(window-numbering
                          :repo "DogLooksGood/window-numbering.el"
                          :host github
                          :type git))


  (require 'window-numbering)
  (window-numbering-mode 1)

  (defun meomacs-window-numbering-modeline ()
    (concat " "
            (let ((n (window-numbering-get-number)))
              (when (numberp n)
                (if window-system
                    (alist-get
                     n
                     '((0 . "0")
                       (1 . "1")
                       (2 . "2")
                       (3 . "3")
                       (4 . "4")
                       (5 . "5")
                       (6 . "6")
                       (7 . "7")
                       (8 . "8")
                       (9 . "9")))
                  (concat "[" (number-to-string n) "] "))))))

  (setq window-numbering-assign-func
        (lambda ()
          (when (string-prefix-p " *Treemacs" (buffer-name))
            9)))

  (let ((modeline-segment '(:eval (meomacs-window-numbering-modeline))))
    (unless (member modeline-segment mode-line-format)
      (setq-default mode-line-format (cons modeline-segment mode-line-format))))
#+end_src

** COMMENT golden-ratio
#+begin_src emacs-lisp
  (straight-use-package 'golden-ratio)

  (define-key mode-specific-map "\\" 'golden-ratio)

  (autoload 'golden-ratio "golden-ratio" nil t)
#+end_src

** ace-window
#+begin_src emacs-lisp
  (straight-use-package 'ace-window)

  (autoload 'ace-swap-window "ace-window" nil t)
  (autoload 'ace-delete-other-windows "ace-window" nil t)

  (keymap-set mode-specific-map "W" 'ace-swap-window)
  (keymap-set mode-specific-map "Q" 'ace-delete-window)
#+end_src

** winner-mode
#+begin_src emacs-lisp
  (require 'winner)

  (winner-mode 1)

  (defun meomacs-single-window-or-undo ()
    (interactive)
    (if (= 1 (length (window-list-1)))
        (winner-undo)
      (delete-other-windows)))
#+end_src

* Workspace management

** COMMENT Using frames

Use frames for workspaces.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-c j") 'select-frame-by-name)

  ;; Better to have title name with project name
  (setq-default frame-title-format
                '((:eval
                   (or (cdr (project-current))
                       (buffer-name)))))
#+end_src

** COMMENT Using tab-bar-mode

Use tabs for workspaces.

#+begin_src emacs-lisp
  ;; We could hide the window decoration
  ;; (setq default-frame-alist '((undecorated . t)))

  (add-hook 'after-init-hook
            (lambda ()
              (tab-rename "*Emacs*")))

  (defun meomacs-format-tab (tab i)
    (let ((current-p (eq (car tab) 'current-tab)))
      (concat
       (propertize (concat
                    " "
                    (alist-get 'name tab)
                    " ")
                   'face
                   (funcall tab-bar-tab-face-function tab))
       " ")))

  (setq tab-bar-border nil
        tab-bar-close-button nil
        tab-bar-new-button (propertize " ðŸž¤ " 'display '(:height 2.0))
        tab-bar-back-button nil
        tab-bar-tab-name-format-function 'meomacs-format-tab
        tab-bar-tab-name-truncated-max 10)

  (tab-bar-mode 1)

  (global-set-key (kbd "C-c j") 'tab-bar-switch-to-tab)
  (global-set-key (kbd "C-<next>") 'tab-bar-switch-to-next-tab)
  (global-set-key (kbd "C-<prior>") 'tab-bar-switch-to-prev-tab)
  (global-set-key (kbd "C-<escape>") 'tab-bar-close-tab)
#+end_src

Add missing keybindings

#+begin_src emacs-lisp
  (global-set-key (kbd "C-x t .") 'tab-bar-rename-tab)
#+end_src

* Auto pairs

** Builtin electric pair

#+begin_src emacs-lisp
  (add-hook 'prog-mode-hook 'electric-pair-local-mode)
  (add-hook 'conf-mode-hook 'electric-pair-local-mode)
#+end_src

** COMMENT Smartparens

Use smartparens for auto pairs, toggle strict mode with =C-c t s=.

#+begin_src emacs-lisp
  (straight-use-package 'smartparens)

  (require 'smartparens)

  (add-hook 'prog-mode-hook 'smartparens-mode)
  (add-hook 'conf-mode-hook 'smartparens-mode)

  (setq sp-highlight-pair-overlay nil
        sp-highlight-wrap-overlay nil)

  (with-eval-after-load "smartparens"

    ;; setup for emacs-lisp
    (sp-with-modes '(emacs-lisp-mode)
      (sp-local-pair "'" nil :actions nil))

    ;; Use strict-mode by default
    (add-hook 'smartparens-mode-hook 'smartparens-strict-mode)

    ;; Keybindings
    (keymap-set prog-mode-map "C-c t s" 'smartparens-strict-mode)
    (keymap-set conf-mode-map "C-c t s" 'smartparens-strict-mode))
#+end_src

* Line numbers with display-line-numbers-mode

#+begin_src emacs-lisp
  (keymap-set global-map "C-c t l" 'display-line-numbers-mode)
#+end_src


* Completion for key sequence

** Which-key
#+begin_src emacs-lisp
  (straight-use-package 'which-key)
  (which-key-mode 1)

  (with-eval-after-load "which-key"
    (diminish 'which-key-mode))
#+end_src

* Minibuffer completion reading
** Vertico & Precient

- Vertico provides a better UX for completion reading.
- Use prescient to support fuzzy search

#+begin_src emacs-lisp
  (straight-use-package '(vertico :files (:defaults "extensions/*")))
  (straight-use-package 'prescient)
  (straight-use-package 'vertico-prescient)

  (require 'vertico)
  (require 'vertico-prescient)

  (vertico-mode 1)
  (vertico-prescient-mode 1)
  (prescient-persist-mode 1)
#+end_src

** COMMENT Selectrum & Precient

#+begin_src emacs-lisp
  (straight-use-package 'selectrum)
  (straight-use-package 'selectrum-prescient)

  (selectrum-mode 1)
  (selectrum-prescient-mode 1)
  (prescient-persist-mode 1)
#+end_src

** Fix M-DEL in minibuffer

Do "delete" instead of "kill" when pressing =M-DEL=.

#+begin_src emacs-lisp
  (defun meomacs-backward-delete-sexp ()
    "Backward delete sexp.

  Used in minibuffer, replace the the default kill behavior with M-DEL."
    (interactive)
    (save-restriction
      (narrow-to-region (minibuffer-prompt-end) (point-max))
      (delete-region
       (save-mark-and-excursion
         (backward-sexp)
         (point))
       (point))))

  (define-key minibuffer-local-map (kbd "M-DEL") #'meomacs-backward-delete-sexp)
#+end_src

* Completion at point

** COMMENT Corfu
#+begin_src emacs-lisp
  (straight-use-package 'corfu)

  (add-hook 'prog-mode-hook 'corfu-mode)
  (add-hook 'conf-mode-hook 'corfu-mode)

  (autoload 'corfu-mode "corfu" nil t)

  (with-eval-after-load "corfu"
    (setq corfu-map (make-keymap))
    (keymap-set corfu-map "M-n" 'corfu-next)
    (keymap-set corfu-map "M-p" 'corfu-previous)
    (setq corfu-auto t
          corfu-preselect 'prompt)
    (dolist (c (list (cons "SPC" " ")
                     (cons "." ".")
                     (cons "," ",")
                     (cons ":" ":")
                     (cons ")" ")")
                     (cons "}" "}")
                     (cons "]" "]")))
      (define-key corfu-map (kbd (car c)) `(lambda ()
                                             (interactive)
                                             (corfu-insert)
                                             (insert ,(cdr c))))))
#+end_src

** Company

#+begin_src emacs-lisp
  (straight-use-package 'company)

  (add-hook 'prog-mode-hook 'company-mode)
  (add-hook 'conf-mode-hook 'company-mode)
  (autoload 'company-mode "company" nil t)

  (setq company-format-margin-function 'company-text-icons-margin
        company-dabbrev-downcase nil
        company-idle-delay 0.1)

  (with-eval-after-load 'company
    (diminish 'company-mode))
#+end_src

A setup for vim-like behavior.  Completion will popup automatically, =SPC= and =RET= will do insertion even though the popup is available.

| action                      | key |
|-----------------------------+-----|
| trigger completion at point | M-n |
| previous candidate          | M-p |
| next candidate              | M-n |
| next template placeholder   | RET |

#+begin_src emacs-lisp
  (with-eval-after-load "company"
    (require 'company-tng)

    (add-hook 'company-mode-hook 'company-tng-mode)

    (keymap-set company-mode-map "M-n" 'company-complete-common)
    (keymap-set company-active-map "TAB" nil)
    (define-key company-active-map [tab] nil)
    (keymap-set company-active-map "C-n" nil)
    (keymap-set company-active-map "C-p" nil)
    (keymap-set company-active-map "M-n" 'company-select-next)
    (keymap-set company-active-map "M-p" 'company-select-previous)

    ;; Free SPC and RET, popup will no longer interrupt typing.
    (define-key company-active-map [escape] nil)
    (define-key company-active-map [return] nil)
    (keymap-set company-active-map "RET" nil)
    (keymap-set company-active-map "SPC" nil))
#+end_src

** COMMENT Company Box

#+begin_src emacs-lisp
  (straight-use-package 'company-box)

  (with-eval-after-load "company"
    (require 'company-box)
    (add-hook 'company-mode-hook 'company-box-mode))
#+end_src

** COMMENT Company posframe

#+begin_src emacs-lisp
  (straight-use-package 'company-posframe)

  (with-eval-after-load "company"
    (require 'company-posframe)
    (add-hook 'company-mode-hook 'company-posframe-mode))
#+end_src

* Templating

** yasnippet

Expand template with =TAB=. Jump between the placeholders with =TAB= and =S-TAB=.

#+begin_src emacs-lisp
  (straight-use-package 'yasnippet)
  (straight-use-package 'yasnippet-snippets)

  (require 'yasnippet)

  (yas-global-mode 1)

  (diminish 'yas-minor-mode)
#+end_src

* Project management

** project.el

To find files/buffers and apply commands on project, use builtin package ~project~.

#+begin_src emacs-lisp
  (when (version<= "28" emacs-version)
    (setq project-switch-commands '((project-find-file "Find file")
                                    (project-find-regexp "Find regexp")
                                    (project-dired "Dired")
                                    (project-eshell "Eshell")
                                    (magit-project-status "Magit")
                                    (shell "Shell")))

    (defalias 'project-prefix-map project-prefix-map)

    (define-key mode-specific-map "p" 'project-prefix-map)

    (with-eval-after-load "project"
      (define-key project-prefix-map "s" 'shell)
      (define-key project-prefix-map "m" 'magit-project-status)))
#+end_src

* Enanced completion commands

** consult

#+begin_src emacs-lisp
  (straight-use-package 'consult)

  (require 'consult)

  (define-key mode-specific-map "G" 'consult-ripgrep)
#+end_src

* File Navigator

** treemacs

#+begin_src emacs-lisp
  (straight-use-package 'treemacs)

  (setq treemacs-is-never-other-window t)
#+end_src

* Text searching

** COMMENT deadgrep

#+begin_src emacs-lisp
  (straight-use-package 'deadgrep)

  (require 'deadgrep)

  (define-key project-prefix-map "r" 'deadgrep)

  (with-eval-after-load "deadgrep"
    (keymap-set deadgrep-mode-map "e" 'deadgrep-edit-mode)
    (add-hook 'deadgrep-mode-hook 'next-error-follow-minor-mode))
#+end_src

** rg.el

#+begin_src emacs-lisp
  (straight-use-package 'rg)

  (autoload 'rg-project "wgrep" nil t)
  (autoload 'rg-project "rg" nil t)

  (with-eval-after-load "rg"
    (rg-enable-default-bindings)
    (rg-enable-menu))

  (with-eval-after-load "wgrep"
    (define-key wgrep-mode-map (kbd "C-c C-c") #'wgrep-finish-edit))

  (define-key project-prefix-map "r" 'rg-project)
#+end_src

* Wrap parentheses
#+begin_src emacs-lisp
  (setq meomacs-wrap-keymap
        (let ((map (make-keymap)))
          (suppress-keymap map)
          (dolist (k '("(" "[" "{" "<"))
            (define-key map k #'insert-pair))
          map))
#+end_src

* TUI Copy
#+begin_src emacs-lisp
  (defun ext-copy (text)
    (cond
     ((string-match-p ".*WSL2" operating-system-release)
      (let ((inhibit-message t)
            (coding-system-for-write 'gbk-dos))
        (with-temp-buffer
          (insert text)
          (call-process-region (point-min) (point-max) "clip.exe" nil 0))))

     ((not window-system)
      (let ((inhibit-message t))
        (with-temp-buffer
          (insert text)
          (call-process-region (point-min) (point-max) "wl-copy" nil 0))))))

  (unless window-system
    (setq interprogram-cut-function 'ext-copy))
#+end_src

* COMMENT Frame associated buffers

#+begin_src emacs-lisp
  (straight-use-package 'beframe)

  (require 'beframe)
  (beframe-mode 1)

  (setq beframe-functions-in-frames '(project-prompt-project-dir))

  (define-key global-map [remap switch-to-buffer] 'beframe-switch-buffer)
#+end_src
