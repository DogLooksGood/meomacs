#+title: Editor Basics

Configuration for editor basics.

#+begin_src emacs-lisp
  ;;; -*- lexical-binding: t -*-
#+end_src

* Package management with Straight

Straight.el is used for package management.
Bind some keys for quick operations.

#+begin_src emacs-lisp
  (defvar straight-keymap
    (let ((keymap (make-keymap)))
      (define-key keymap "c" 'straight-check-package)
      (define-key keymap "C" 'straight-check-all)
      (define-key keymap "p" 'straight-pull-package)
      (define-key keymap "P" 'straight-pull-all)
      (define-key keymap "b" 'straight-rebuild-package)
      (define-key keymap "B" 'straight-rebuild-all)
      keymap))

  (defalias 'straight-keymap straight-keymap)
  (global-set-key (kbd "C-c p") 'straight-keymap)
#+end_src

* Better defaults

#+begin_src emacs-lisp
  (setq-default
   ;; Don't create lockfiles
   create-lockfiles nil

   ;; prefer UTF8
   buffer-file-coding-system 'utf-8-unix
   default-file-name-coding-system 'utf-8-unix
   default-keyboard-coding-system 'utf-8-unix
   default-process-coding-system '(utf-8-unix . utf-8-unix)
   default-sendmail-coding-system 'utf-8-unix
   default-terminal-coding-system 'utf-8-unix

   ;; Add newline at bottom of file
   require-final-newline t

   ;; Backup setups
   ;; We use temporary directory /tmp for backup files
   ;; More versions should be saved
   backup-directory-alist `((".*" . ,temporary-file-directory))
   auto-save-file-name-transforms `((".*" ,temporary-file-directory t))
   backup-by-copying t
   delete-old-versions t
   kept-new-versions 6
   kept-old-versions 2
   version-control t

   ;; Skip prompt for xref find definition
   xref-prompt-for-identifier nil

   ;; Don't wait for keystrokes display
   echo-keystrokes 0.01

   ;; Disable margin for overline and underline
   overline-margin 0
   underline-minimum-offset 0

   ;; Allow resizing frame by pixels
   frame-resize-pixelwise t

   ;; Better scroll behavior
   mouse-wheel-scroll-amount '(1 ((shift) . 1) ((control) . nil))
   mouse-wheel-progressive-speed nil

   ;; Disable copy region blink
   copy-region-blink-delay 0

   ;; Use short answer when asking yes or no
   read-answer-short t

   ;; Always follow link
   vc-follow-symlinks t

   ;; Use custom.el as custom file
   custom-file (expand-file-name "custom.el" user-emacs-directory)

   ;; Disable ring bell
   ring-bell-function 'ignore

   ;; Mouse yank at current point
   mouse-yank-at-point t

   ;; DWIM target for dired
   ;; Automatically use another dired buffer as target for copy/rename
   dired-dwim-target t)

  (global-hl-line-mode t)
#+end_src

* Modal editing with Meow

This configuration sticks with Meow for modal editing.

** Normal
Tweaking following table for key bindings in normal state.

#+TBLNAME: normal-keybindings
| key      | command                   |
|----------+---------------------------|
| ?        | meow-keypad-describe-key  |
| *        | meow-expand-0             |
| =        | meow-expand-9             |
| !        | meow-expand-8             |
| [        | meow-expand-7             |
| ]        | meow-expand-6             |
| {        | meow-expand-5             |
| +        | meow-expand-4             |
| }        | meow-expand-3             |
| )        | meow-expand-2             |
| (        | meow-expand-1             |
| 1        | select-window-1           |
| 2        | select-window-2           |
| 3        | select-window-3           |
| 4        | select-window-4           |
| 5        | select-window-5           |
| 6        | select-window-6           |
| 7        | select-window-7           |
| 8        | select-window-8           |
| 9        | select-window-9           |
| 0        | select-window-0           |
| -        | negative-argument         |
| ;        | meow-reverse              |
| ,        | meow-inner-of-thing       |
| .        | meow-bounds-of-thing      |
| <        | meow-beginning-of-thing   |
| >        | meow-end-of-thing         |
| a        | meow-append               |
| A        | meow-open-below           |
| b        | meow-back-word            |
| B        | meow-back-symbol          |
| c        | meow-change               |
| d        | meow-delete               |
| D        | meow-backward-delete      |
| e        | meow-line                 |
| E        | meow-goto-line            |
| f        | meow-find                 |
| g        | meow-cancel-selection     |
| G        | meow-grab                 |
| h        | meow-left                 |
| H        | meow-left-expand          |
| i        | meow-insert               |
| I        | meow-open-above           |
| j        | meow-join                 |
| k        | meow-kill                 |
| l        | meow-till                 |
| m        | meow-mark-word            |
| M        | meow-mark-symbol          |
| n        | meow-next                 |
| N        | meow-next-expand          |
| o        | meow-block                |
| O        | meow-to-block             |
| p        | meow-prev                 |
| P        | meow-prev-expand          |
| q        | meow-quit                 |
| r        | meow-replace              |
| R        | meow-swap-grab            |
| s        | meow-search               |
| t        | meow-right                |
| T        | meow-right-expand         |
| u        | meow-undo                 |
| U        | meow-undo-in-selection    |
| v        | meow-visit                |
| w        | meow-next-word            |
| W        | meow-next-symbol          |
| x        | meow-save                 |
| X        | meow-sync-grab            |
| y        | meow-yank                 |
| z        | meow-pop-selection        |
| $        | repeat                    |
| '        | repeat                    |
| &        | meow-query-replace-regexp |
| %        | meow-query-replace        |
| <escape> | ignore                    |

** Motion

Tweaking following table for key bindings in motion state.

#+TBLNAME: motion-keybindings
|      key | command         |
|----------+-----------------|
| <escape> | ignore          |
|        1 | select-window-1 |
|        2 | select-window-2 |
|        3 | select-window-3 |
|        4 | select-window-4 |
|        5 | select-window-5 |
|        6 | select-window-6 |
|        7 | select-window-7 |
|        8 | select-window-8 |
|        9 | select-window-9 |
|        0 | select-window-0 |

** Leader

Tweaking following table for key bindings in leader keymap.

#+tblname: leader-keybindings
| key | command                 | description |
|-----+-------------------------+-------------|
| e   | dispatch: C-x C-e       |             |
| s   | split-window-right      |             |
| v   | split-window-below      |             |
| p   | project-map             |             |
| w   | other-window            |             |
| i   | imenu                   |             |
| b   | switch-to-buffer        |             |
| o   | delete-other-windows    |             |
| k   | kill-current-buffer     |             |
| u   | meow-universal-argument |             |
#+TBLFM: 

** Initialization

#+header: :var normal-keybindings=normal-keybindings :var motion-keybindings=motion-keybindings :var leader-keybindings=leader-keybindings
#+begin_src emacs-lisp
  (straight-use-package 'meow)

  (require 'meow)

  (let ((parse-def (lambda (x)
                     (cons (format "%s" (car x))
                           (if (string-prefix-p "dispatch:" (cadr x))
                               (string-trim (substring (cadr x) 9))
                             (intern (cadr x)))))))
    (apply #'meow-normal-define-key (mapcar parse-def normal-keybindings))
    (apply #'meow-motion-overwrite-define-key (mapcar parse-def motion-keybindings))
    (apply #'meow-leader-define-key (mapcar parse-def leader-keybindings)))

  (when window-system
    (setq meow-replace-state-name-list
          '((normal . "üÖù")
            (beacon . "üÖë")
            (insert . "üÖò")
            (motion . "üÖú")
            (keypad . "üÖö"))))

  (setq meow-esc-delay 0.001)

  (unless (bound-and-true-p meow-global-mode)
    (meow-global-mode 1))

  (meow-setup-indicator)
#+end_src

* Window management

Switch window by pressing the number showned in the mode line.

#+begin_src emacs-lisp
  (straight-use-package '(window-numbering
                          :repo "DogLooksGood/window-numbering.el"
                          :host github
                          :type git))


  (require 'window-numbering)
  (window-numbering-mode 1)

  (defun meomacs-window-numbering-modeline ()
    (concat " "
            (let ((n (window-numbering-get-number)))
              (if window-system
                  (alist-get
                   n
                   '((0 . "‚ì™")
                     (1 . "‚ë†")
                     (2 . "‚ë°")
                     (3 . "‚ë¢")
                     (4 . "‚ë£")
                     (5 . "‚ë§")
                     (6 . "‚ë•")
                     (7 . "‚ë¶")
                     (8 . "‚ëß")
                     (9 . "‚ë®")))
                (number-to-string n)))))

  (let ((modeline-segment '(:eval (meomacs-window-numbering-modeline))))
    (unless (member modeline-segment mode-line-format)
      (setq-default mode-line-format (cons modeline-segment mode-line-format))))
#+end_src

* Workspace management with frames

Use frames for workspaces.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-c j") 'select-frame-by-name)

  ;; Better to have title name with project name
  (setq-default frame-title-format
                '((:eval
                   (or (cdr (project-current))
                       (buffer-name)))))
#+end_src

* COMMENT Workspace management with tab-bar-mode

Use tabs for workspaces.

#+begin_src emacs-lisp
  (defun meomacs-format-tab (tab i)
    (let ((current-p (eq (car tab) 'current-tab)))
      (concat
       (propertize (concat
                    " "
                    (alist-get 'name tab)
                    " ")
                   'face
                   (funcall tab-bar-tab-face-function tab))
       " ")))

  (setq tab-bar-border nil
        tab-bar-close-button nil
        tab-bar-new-button (propertize " üû§ " 'display '(:height 2.0))
        tab-bar-back-button nil
        tab-bar-tab-name-format-function 'meomacs-format-tab
        tab-bar-tab-name-truncated-max 10)

  (tab-bar-mode 1)

  (global-set-key (kbd "C-c j") 'tab-bar-switch-to-tab)
#+end_src

Add missing keybindings

#+begin_src emacs-lisp
  (global-set-key (kbd "C-x t .") 'tab-bar-rename-tab)
#+end_src
* Auto pairs with Smartparens

Use smartparens for auto pairs, toggle strict mode with =C-c t s=.

#+begin_src emacs-lisp
  (straight-use-package 'smartparens)

  (require 'smartparens)

  (add-hook 'prog-mode-hook 'smartparens-mode)
  (add-hook 'conf-mode-hook 'smartparens-mode)

  (setq sp-highlight-pair-overlay nil
        sp-highlight-wrap-overlay nil)

  (with-eval-after-load "smartparens"

    ;; setup for emacs-lisp
    (sp-with-modes '(emacs-lisp-mode)
      (sp-local-pair "'" nil :actions nil))

    ;; Use strict-mode by default
    (add-hook 'smartparens-mode-hook 'smartparens-strict-mode)

    ;; Keybindings
    (define-key toggle-map "s" 'smartparens-strict-mode))
#+end_src

* Line numbers with display-line-numbers-mode

#+begin_src emacs-lisp
  (define-key toggle-map "l" 'display-line-numbers-mode)
#+end_src


* Completions

** Key sequence completion with which-key
#+begin_src emacs-lisp
(straight-use-package 'which-key)
(which-key-mode 1)
#+end_src

** Minibuffer completion reading with Vertico & Orderless & Marginalia

- Vertico provides a better UX for completion reading.
- Orderless provides a completion style, which allows you to search with orderless segments.
- Marginalia provides helpful messages in completion.

#+begin_src emacs-lisp
  (straight-use-package 'vertico)
  (straight-use-package 'orderless)

  (require 'vertico)
  (require 'orderless)

  (vertico-mode 1)
#+end_src

Enable orderless for minibuffer completion.

#+begin_src emacs-lisp
  (defun meomacs--vertico-init-minibuffer ()
    (setq-local completion-styles '(basic orderless)))

  (add-hook 'minibuffer-setup-hook 'meomacs--vertico-init-minibuffer)
#+end_src

Do "delete" instead of "kill" when pressing =M-DEL=.

#+begin_src emacs-lisp
  (defun meomacs-backward-delete-sexp ()
    "Backward delete sexp.

  Used in minibuffer, replace the the default kill behavior with M-DEL."
    (interactive)
    (save-restriction
      (narrow-to-region (minibuffer-prompt-end) (point-max))
      (delete-region
       (save-mark-and-excursion
         (backward-sexp)
         (point))
       (point))))

  (define-key minibuffer-mode-map (kbd "M-DEL") #'meomacs-backward-delete-sexp)
#+end_src

** Completion at point

#+begin_src emacs-lisp
  (add-hook 'prog-mode-hook 'company-mode)
  (add-hook 'conf-mode-hook 'company-mode)
#+end_src

** Completion at point with Company

#+begin_src emacs-lisp
  (straight-use-package 'company)

  (autoload 'company-mode "company" nil t)
#+end_src

A setup for vim-like behavior.  Completion will popup automatically, =SPC= and =RET= will do insertion even though the popup is available.

| action                    | key |
|---------------------------+-----|
| completion at point       | TAB |
| previous candidate        | C-p |
| next candidate            | C-n |
| next template placeholder | RET |

#+begin_src emacs-lisp
  (with-eval-after-load "company"
    (require 'company-tng)

    (add-hook 'company-mode-hook 'company-tng-mode)

    (define-key company-mode-map (kbd "M-n") 'company-complete-common)
  
    (define-key company-active-map (kbd "TAB") nil)
    (define-key company-active-map [tab] nil)
    (define-key company-active-map (kbd "C-n") nil)
    (define-key company-active-map (kbd "C-p") nil)
    (define-key company-active-map (kbd "M-n") 'company-select-next)
    (define-key company-active-map (kbd "M-p") 'company-select-previous)

    ;; Free SPC and RET, popup will no longer interrupt typing.
    (define-key company-active-map [escape] nil)
    (define-key company-active-map [return] nil)
    (define-key company-active-map (kbd "RET") nil)
    (define-key company-active-map (kbd "SPC") nil))
#+end_src

* Templating with yasnippet
Expand template with =TAB=. Jump between the placeholders with =TAB= and =S-TAB=.

#+begin_src emacs-lisp
  (straight-use-package 'yasnippet)
  (straight-use-package 'yasnippet-snippets)

  (require 'yasnippet)

  (yas-global-mode 1)
#+end_src

* Project management with project.el

To find files/buffers and apply commands on project, use builtin package ~project~.

#+begin_src emacs-lisp
  (setq project-switch-commands '((project-find-file "Find file")
                                  (project-find-regexp "Find regexp")
                                  (project-dired "Dired")
                                  (project-eshell "Eshell")
                                  (shell "Shell")
                                  (magit "Magit")))

  (defalias 'project-map project-prefix-map)

  (with-eval-after-load "project"
    (define-key project-prefix-map "s" 'shell)
    (define-key project-prefix-map "m" 'magit))
#+end_src

* Searching with rg
#+begin_src emacs-lisp
  (straight-use-package 'rg)

  (autoload 'rg-project "wgrep" nil t)
  (autoload 'rg-project "rg" nil t)

  (with-eval-after-load "wgrep"
    (define-key wgrep-mode-map (kbd "C-c C-c") #'wgrep-finish-edit))

  (define-key project-prefix-map "r" 'rg-project)
#+end_src
