#+title: Look & Feel

Configuration for look and feels, make UI looks better.

#+begin_src emacs-lisp
  ;;; -*- lexical-binding: t -*-
#+end_src

* Disabling some components

No tool bar & menu bar.

#+begin_src emacs-lisp
  (tool-bar-mode -1)
  (menu-bar-mode -1)
#+end_src

No scroll bars in minibuffer.

#+begin_src emacs-lisp
  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (set-window-scroll-bars
               (minibuffer-window frame) 0 nil 0 nil t)))
#+end_src

* Enable window divider
#+begin_src emacs-lisp
  (window-divider-mode 1)
#+end_src

* Some UI related variables

#+begin_src emacs-lisp
  (setq x-underline-at-descent-line t)
#+end_src

* Fonts

** Font Families

Default variables

#+begin_src emacs-lisp
  (defvar meomacs-font-size 12
    "Current font size.")

  (defvar meomacs-fonts '((default . "Monospace")
                          (cjk . "Unifont")
                          (symbol . "Unifont")
                          (fixed . "Monospace")
                          (fixed-serif . "Monospace Serif")
                          (variable . "Sans Serif")
                          (wide-variant . "Monospace")
                          (tall-variant . "Monospace"))
    "Fonts to use.")
#+end_src

Define functions for loading fonts.
Font families are specified in [[file:private.org::Fonts][private]] config.

#+begin_src emacs-lisp
  (defun meomacs--get-font-family (key)
    (alist-get key meomacs-fonts))

  (defun meomacs-load-default-font ()
    "Load default font configuration."
    (let ((default-font (format "%s-%s"
                                (meomacs--get-font-family 'default)
                                meomacs-font-size)))
      (add-to-list 'default-frame-alist (cons 'font default-font))))

  (defun meomacs-load-face-font ()
    "Load face font configuration."
    (let ((variable-font (meomacs--get-font-family 'variable))
          (fixed-font (meomacs--get-font-family 'fixed))
          (fixed-serif-font (meomacs--get-font-family 'fixed-serif)))
      (set-face-attribute 'variable-pitch nil :family variable-font)
      (set-face-attribute 'fixed-pitch nil :family fixed-font)
      (set-face-attribute 'fixed-pitch-serif nil :family fixed-serif-font)))

  (defun meomacs-load-charset-font (&optional font)
    "Load charset font configuration."
    (let ((default-font (or font (format "%s-%s"
                                         (meomacs--get-font-family 'default)
                                         meomacs-font-size)))
          (cjk-font (meomacs--get-font-family 'cjk))
          (symbol-font (meomacs--get-font-family 'symbol)))
      (set-frame-font default-font)
      (let ((fontset (create-fontset-from-ascii-font default-font)))
        ;; Fonts for charsets
        (dolist (charset '(kana han hangul cjk-misc bopomofo))
          (set-fontset-font fontset charset cjk-font))
        (set-fontset-font fontset 'symbol symbol-font)
        ;; Apply fontset
        (set-frame-parameter nil 'font fontset)
        (add-to-list 'default-frame-alist (cons 'font fontset)))))

  (meomacs-load-default-font)
  (meomacs-load-face-font)

  ;; Run after startup
  (add-hook 'after-init-hook
            (lambda ()
              (when window-system
                (meomacs-load-charset-font))))
#+end_src

*** Variants used when spliting window
#+begin_src emacs-lisp
  (defvar meomacs-font-current-variant nil)

  (defun meomacs-dynamic-set-font (&rest ignore)
    (interactive)
    (when (or (frame-root-window-p (get-buffer-window))
              (frame-root-window-p (window-parent)))
      (let* ((prev-font-style meomacs-font-current-variant)
             (wl (seq-filter (lambda (w) (not (string-prefix-p " " (buffer-name (window-buffer w))))) (window-list)))
             (new-variant (cond
                              ((= 1 (length wl))
                               (meomacs--get-font-family 'default))

                              ((window-combined-p)
                               (meomacs--get-font-family 'tall))

                              (t
                               (meomacs--get-font-family 'wide)))))
        (unless (equal prev-font-style new-variant)
          (setq meomacs-font-current-variant new-variant)
          (set-frame-font new-variant)
          (meomacs-load-charset-font new-variant)))))

  (setq frame-inhibit-implied-resize t)
  (add-hook 'window-state-change-hook 'meomacs-dynamic-set-font)
#+end_src

* Themes

Create a command ~meomacs-next-theme~ at =C-c t t= to switch themes.

#+begin_src emacs-lisp
  (require 'subr-x)
  (require 'cl-lib)

  (defvar meomacs-themes '()
    "Themes to use.")

  (defun meomacs-load-theme ()
    (when-let ((theme (car meomacs-themes)))
      (message "Load theme: %s" (car meomacs-themes))
      (mapc 'disable-theme custom-enabled-themes)
      (unless (eq theme 'default)
        (load-theme theme t))))

  (defun meomacs-next-theme ()
    (interactive)
    (when meomacs-themes
      (setq meomacs-themes (append (cdr meomacs-themes) (list (car meomacs-themes))))
      (meomacs-load-theme)))

  (define-key toggle-map "t" 'meomacs-next-theme)

  (add-hook 'desktop-after-read-hook 'meomacs-load-theme)
#+end_src

* Modeline

** Use variable font

#+begin_src emacs-lisp
  (custom-set-faces
   '(mode-line ((t :inherit variable-pitch)))
   '(mode-line-inactive ((t :inherit variable-pitch))))
#+end_src

** Diminish
Hide unnecessary lighters.
#+begin_src emacs-lisp
  (straight-use-package 'diminish)

  (require 'diminish)
  (diminish 'gcmh-mode)
  (diminish 'buffer-face-mode)
  (diminish 'eldoc-mode)
#+end_src

** COMMENT mood-line
#+begin_src emacs-lisp
  (straight-use-package 'mood-line)

  (require 'mood-line)
  (mood-line-mode 1)
#+end_src
